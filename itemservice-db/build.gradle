plugins {
	id 'org.springframework.boot' version '2.6.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext["hibernate.version"] = "5.6.5.Final"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
//	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	/**
	 * MyBatis 추가 - Spring 이 공식적으로 관리하지 않는 라이브러리는 버전 정보 필요
	 *
	 * mybatis-spring-boot-starter : MyBatis를 스프링 부트에서 편리하게 사용할 수 있게 시작하는 라이브러리
	 * mybatis-spring-boot-autoconfigure : MyBatis와 스프링 부트 설정 라이브러리
	 * mybatis-spring : MyBatis와 스프링을 연동하는 라이브러리
	 * mybatis : MyBatis 라이브러리
	 */
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
	/**
	 * JPA, Spring Data JPA - JPA, Spring Data JPA 를 Spring Boot 와 통합
	 *
	 * hibernate-core : JPA 구현체인 하이버네이트 라이브러리
	 * jakarta.persistence-api : JPA 인터페이스
	 * spring-data-jpa : 스프링 데이터 JPA 라이브러리
	 */
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Querydsl
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	//테스트에서 lombok 사용
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

// IntelliJ Build 에서 자동 생성된 QClass를 gradle clean으로 제거
clean {
	delete file('src/main/generated')
}